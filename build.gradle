import org.gradle.api.publish.maven.MavenPublication

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'versions'

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = 1
group = 'de.gesellix'
archivesBaseName = 'gradle-debian-plugin'

buildscript {
  repositories {
    maven {
      url 'http://jcenter.bintray.com'
    }
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.4'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
  }
}
apply plugin: 'bintray'

repositories {
  mavenCentral()
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()

    def dependencyVersions = [
        'org.codehaus.plexus:plexus-classworlds': '1.2-alpha-9',
        'org.codehaus.plexus:plexus-container-default': '1.0-alpha-30',
        'org.codehaus.plexus:plexus-interactivity-api': '1.0-alpha-6',
        'org.codehaus.plexus:plexus-utils': '1.5.15',
        'classworlds:classworlds': '1.1',
        'commons-httpclient:commons-httpclient': '3.1',
        'org.hamcrest:hamcrest-core': '1.3',
        'org.slf4j:slf4j-api': '1.5.6'
    ]

    force dependencyVersions.collect { k, v -> "$k:$v" }
  }
}

dependencies {
  compile gradleApi()
  compile localGroovy()
//  groovy 'org.codehaus.groovy:groovy-all:2.0.5'

  compile 'org.vafer:jdeb:1.0.1'

  testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
}

task testng(type: Test) {
  useTestNG()
}
check.dependsOn testng

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: 'file:./lib')
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java  // jar
    }
  }
}

ext.dry = false
bintray {
  user = project["bintray.user"]
  key = project["bintray.key"]
  publications = ['mavenJava']
  pkg {
    repo = 'gradle-plugins'
    //userOrg = 'myorg' // an optional organization name when the repo belongs to one of the user's orgs
    name = 'gradle-debian-plugin'
    desc = 'create debian packages with gradle'
    licenses = ['Apache-2.0']
    labels = []
  }
  dryRun = dry
}
bintrayUpload.dependsOn(":build")

task wrapper(type: Wrapper) {
  gradleVersion = '1.8'
}
